mdl 1.6;

using ::df import *;
using ::state import *;
using ::base import *;
using ::tex import *;
using ::anno import *;
using ::math import *;
using ::limits import *;

//- Custom annotation for Substance Designer specifics
import ::alg::base::annotations::*;

//- Core functions and materials for Substance Designer specifics
import ::alg::base::core::*;

//- Import base material
import ::alg::materials::designer::legacy::physically_metallic_roughness::physically_metallic_roughness;

export material physically_metallic_roughness_sss(

	varying color baseColor = color(0.214041) [[
		anno::display_name("Base Color/Diffuse")
		, anno::in_group("Base Color")
		, anno::description("Base Color channel")
		, anno::usage("baseColor")
		, alg::base::annotations::gamma_type(gamma_srgb)
		, alg::base::annotations::visible_by_default(true)
	]],

	varying float3 normal = state::normal() [[
		anno::display_name("Normal")
		, anno::in_group("Normal")
		, anno::description("Normal channel")
		, anno::usage("normal")
		, alg::base::annotations::gamma_type(::tex::gamma_linear)
		, alg::base::annotations::visible_by_default(true)
	]],

	varying float roughness = float(0.5) [[
		anno::display_name("Roughness")
		, anno::in_group("Roughness")
		, anno::description("Roughness channel")
		, anno::usage("roughness")
		, alg::base::annotations::gamma_type(::tex::gamma_linear)
		, alg::base::annotations::visible_by_default(true)
	]],

	varying float metallic = float(0) [[
		anno::display_name("Metallic")
		, anno::in_group("Metallic")
		, anno::description("Metallic channel")
		, anno::usage("metallic")
		, alg::base::annotations::gamma_type(::tex::gamma_linear)
		, alg::base::annotations::visible_by_default(true)
	]],

	varying float height = float(0.5) [[
		anno::display_name("Height")
		, anno::in_group("Height")
		, anno::description("Height channel")
		, anno::usage("height")
		, alg::base::annotations::gamma_type(::tex::gamma_linear)
		, alg::base::annotations::visible_by_default(true)
	]],

	uniform float heightScale = float(0) [[
		anno::display_name("Scale")
		, anno::in_group("Height")
		, anno::description("provides significant physical detail to the surface by manipulating the vertices of the mesh")
		, anno::usage("heightScale")
		, alg::base::annotations::visible_by_default(false)
	]],

    uniform float ior = float(1.5) [[
        anno::display_name("IOR")
		, anno::in_group("Subsurface Scattering")
		, anno::description("Desrcibes the index of refraction of the material medium")
    ]],

	varying float scattering = float(0) [[
		anno::display_name("Scattering")
		, anno::in_group("Subsurface Scattering")
		, anno::usage("scattering")
		, anno::description("How much to blend the SSS material to take into account vs. the usual PBR shader")
		, alg::base::annotations::visible_by_default(true)
        , alg::base::annotations::gamma_type(::tex::gamma_linear)
	]],

	uniform float scatteringScale = float(0) [[
		anno::display_name("Scale")
		, anno::in_group("Subsurface Scattering")
		, anno::description("Artistict control to tweak the subsurface scattering effect")
		, anno::soft_range(0.0, 10.0)
		, alg::base::annotations::visible_by_default(false)
	]],

	uniform color scatteringColor = color(1) [[
		anno::display_name("Color")
		, anno::in_group("Subsurface Scattering")
		, anno::description("Describes at which probability each wavelength scatters within the volume")
		, alg::base::annotations::visible_by_default(false)
	]],

	varying float ambientOcclusion = float(1) [[
		anno::display_name("Ambient Occlusion")
		, anno::in_group("Ambient Occlusion")
		, anno::description("Ambient Occlusion channel")
		, anno::usage("ambientOcclusion")
		, alg::base::annotations::gamma_type(::tex::gamma_linear)
		, alg::base::annotations::visible_by_default(false)
	]],

	varying float anisotropyLevel = float(0) [[
		anno::display_name("Anisotropy Level")
		, anno::in_group("Anisotropy")
		, anno::description("Anisotropy Level channel")
		, anno::usage("anisotropyLevel")
		, alg::base::annotations::gamma_type(::tex::gamma_linear)
		, alg::base::annotations::visible_by_default(false)
	]],

	varying float anisotropyAngle = float(0) [[
		anno::display_name("Anisotropy Angle")
		, anno::in_group("Anisotropy")
		, anno::description("Anisotropy Angle channel")
		, anno::usage("anisotropyAngle")
		, alg::base::annotations::gamma_type(::tex::gamma_linear)
		, alg::base::annotations::visible_by_default(false)
	]],

	varying color emissiveColor = color(0) [[
		anno::display_name("Emissive")
		, anno::in_group("Emissive")
		, anno::description("Emissive channel")
		, anno::usage("emissive")
		, alg::base::annotations::gamma_type(gamma_srgb)
		, alg::base::annotations::visible_by_default(false)
	]],

	uniform float emissiveIntensity = float(1) [[
		anno::display_name("Emissive Intensity")
		, anno::in_group("Emissive")
		, anno::description("Controls how much light is emitted from the surface")
		, alg::base::annotations::visible_by_default(false)
	]],

	varying float specularLevel = float(0.5) [[
		anno::display_name("Specular Level")
		, anno::in_group("Specular")
		, anno::description("Specular Level channel")
		, anno::usage("specularLevel")
		, alg::base::annotations::gamma_type(::tex::gamma_linear)
		, alg::base::annotations::visible_by_default(false)
	]],

    uniform float scalarZeroValue = float(0.5) [[
        anno::display_name("Scalar Zero Value")
        , anno::in_group("Height")
        , anno::usage("heightScalarZeroValue")
        , anno::soft_range(0.0, 1.0)
        , anno::description("Determine the neutral height value")
    ]],

    uniform float tiling = float(1.0) [[
        anno::display_name("Tiling")
        , anno::in_group("Height")
        , anno::usage("materialTiling")
        , anno::soft_range(0.0, 10.0)
        , anno::description("Tiling value to keep displacement tiling independent")
    ]]

) [[
    anno::display_name("Physically Metallic Roughness - SSS")
    , anno::description("Mixture of the usual PBR shader with a Subsurface scattering one")
    , anno::version( 2020, 8, 21 )
    , anno::created( 2019, 8, 12, "Built as a blend with alg::materials::physically_metallic_roughness::physically_metallic_roughness" )
    , anno::modified(2020, 8, 21, "Added scalarZeroValue to be able to interpret different height texture correctly")
    , anno::modified(2020, 8, 21, "Added tiling to be able to express displacement in a tiling independent manner")
    , anno::author(alg::base::core::ALLEGORITHMIC_AUTHOR)
    , anno::copyright_notice(alg::base::core::ALLEGORITHMIC_COPYRIGHT)
    , anno::hidden()
]] = let{

	material base_material = ::alg::materials::designer::legacy::physically_metallic_roughness::physically_metallic_roughness(
		ambientOcclusion  : ambientOcclusion ,
		baseColor         : baseColor        ,
		emissiveColor     : emissiveColor    ,
		emissiveIntensity : emissiveIntensity,
		height            : height           ,
		heightScale       : heightScale      ,
		metallic          : metallic         ,
		normal            : normal           ,
		roughness         : roughness        ,
		/// NOTE opacity is incompatible with SSS effect
		opacity           : 1.0              ,
		specularLevel     : specularLevel    ,
		anisotropyLevel   : anisotropyLevel  ,
		anisotropyAngle   : anisotropyAngle  ,
		refraction        : 0.0              ,
		refractionIOR     : ior              ,
		scattering        : 0.0              ,
		absorption        : 0.0              ,
		absorptionColor   : color(0.0)       ,
        scalarZeroValue   : scalarZeroValue  ,
        tiling            : tiling
	);

    //-Skin

    color diffuse_bsdf_tint = base::blend_color_layers(
        base: color(1.0),
        layers: base::color_layer[](
            base::color_layer( //normalize the color
                   layer_color: baseColor,
                   mode: base::color_layer_color)
               )
        ).tint;

    float diffuse_weight = 0.0;
    float diffuse_bsdf_weight = base::blend_color_layers(
        base: color(0.0),
        layers: base::color_layer[](
            base::color_layer(  //color*weight
                layer_color: baseColor,
                weight: math::sqrt(diffuse_weight),
                mode: base::color_layer_blend
                ),
            base::color_layer( //make greyscale
                layer_color: color(1.0),
                weight: 1.0,
                mode: base::color_layer_color
                )
            )
        ).mono;

    /*0.04 is the equivalent of ior 1.5 */
    float gain = 1.0;
    float specular_weight = 1.0;
    float specular_weight_2 = 1.0 - math::pow(specularLevel, gain)*0.04*specular_weight;

	//- NOTE: Do not forget to square roughness to make it perceive as linear progression
    float specular_roughness_1 = roughness*roughness;
    float specular_roughness_2 = specular_roughness_1*0.25; //.25 is a heuristic

    //taking inspiration from http://www.iryoku.com/stare-into-the-future
    //2 lobes
    //cavity fadeout towards edges

    bsdf skin_specular_1 = df::microfacet_ggx_smith_bsdf(roughness_u: specular_roughness_1);
    bsdf skin_specular_2 = df::microfacet_ggx_smith_bsdf(roughness_u: specular_roughness_2);

    float specular_roughness_ratio = 0.85;
    bsdf skin_specular_combined = df::weighted_layer(
        weight: specular_roughness_ratio,
        layer: skin_specular_1,
        base: df::weighted_layer(
            weight: 1.0,
            layer: skin_specular_2,
        	normal: normal
        ),
        normal: normal
    );

    //note since this might be counter intuitive: diffuse reflection is happening when light enters as well as leaves the volume!!
    //one way to make this more intuitive might be to sqrt the diffuse weight

    color transmission_color =  math::sqrt(baseColor);

    bsdf skin_base = df::weighted_layer(
        weight: math::sqrt(diffuse_bsdf_weight),
    	///- BRDF Diffuse
        layer: df::diffuse_reflection_bsdf (tint: diffuse_bsdf_tint),
        base: df::weighted_layer(
        	weight: 1.0,
    		///- BTDF
        	layer: df::diffuse_transmission_bsdf( tint: transmission_color ),
        	normal: normal
        ),
        normal: normal
    );

    bsdf skin_bsdf = df::custom_curve_layer(
        normal_reflectivity: 1-specular_weight_2,
        grazing_reflectivity: specular_weight,
    	///- BRDF Specular
        layer: skin_specular_combined,
        base: skin_base
        /// NOTE normal already taken into account in skin_specular_combined
    );

    //- Volume Scattering

    //- denominator always != 0.0
    float sssScaleFactor = 0.333f;
    float sssScaleMatchingOpenGL = math::max( scatteringScale * sssScaleFactor, 0.001 );

    float sssColorSaturation = 0.85;
    color adjusted_sss_color = ( scatteringColor - color( 0.5 ) ) * sssColorSaturation + color( 0.5 );

    //- Volume Coefficients
    color absorption_coef = math::log( adjusted_sss_color ) / -(sssScaleMatchingOpenGL*100.0);
    color scattering_coef = math::log( adjusted_sss_color ) / -(sssScaleMatchingOpenGL);

} in material(
	ior: color(ior),
	surface: material_surface(
		scattering: df::weighted_layer(
            weight: scattering,
            layer: skin_bsdf,
            base: base_material.surface.scattering
        ),
		emission: base_material.surface.emission
	),

	volume: material_volume(
        absorption_coefficient: absorption_coef,
        scattering_coefficient: scattering_coef
    ),

	geometry: base_material.geometry
);
